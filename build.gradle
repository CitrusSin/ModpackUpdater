plugins {
    id 'java'
    id 'application'
}

group 'io.github.citrussin.modupdater'
version '1.5.001'

sourceCompatibility = 1.8
mainClassName = 'io.github.citrussin.modupdater.ModUpdaterMain'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'commons-logging:commons-logging:1.2'
    implementation 'org.apache.httpcomponents:httpcore:4.4.15'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'com.google.code.gson:gson:2.10.1'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': mainClassName
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task runClient(type: Exec) {
    dependsOn build
    commandLine "java","-classpath", sourceSets.main.runtimeClasspath.getAsPath(), mainClassName
}

task runServer(type: Exec) {
    dependsOn build
    commandLine "java","-classpath", sourceSets.main.runtimeClasspath.getAsPath(), mainClassName, "--server"
}

clean.doLast {
    file("mods").deleteDir()
    file("clientMods").deleteDir()
    file(".minecraft").deleteDir()
    file("modupdater_client_config.json").delete()
    file("modupdater_server_config.json").delete()
    file("modupdater_redirection_list.json").delete()
}
